<start>         ::= <[directives]> <[codelines]>
<[directives]>  ::= <directive> | <directive> <nl+> <[directives]> | <[\n]>
<directive>     ::= ".TITLE" <s+> <quoted_string>
<directive>     ::= ".NAME" <s+> <identifier> <s+> <number>
<directive>     ::= ".FREQUENCY" <s+> <dec+>
<directive>     ::= ".SIMDIP" <s+> <n>
<directive>     ::= ".DATA" <s+> <n> <s+> <array>
<[codelines]>   ::= <codeline> | <codeline> <nl+> <[codelines]> | <[\n]>
<codeline>      ::= <label> | <instruction> | <label> <[\n]> <instruction>
<label>         ::= <identifier> <s*> ":"
<instruction>   ::= <instr_noarg>
<instruction>   ::= <instr_reg> <s+> <reg>
<instruction>   ::= <instr_n> <s+> <n>
<instruction>   ::= <instr_op1> <s+> <op1>
<instruction>   ::= <instr_reg_op2> <s+> <reg> <,> <op2>
<instruction>   ::= <instr_ldst> <s+> <reg> <,> <bracket_op2>
<instruction>   ::= <instr_reg_n> <s+> <reg> <,> <n>
<instr_noarg>   ::= "HLT" | "NOP" | "RETURN"
<instr_reg>     ::= "RSHIFT" | "LSHIFT" | "PUSH" | "POP"
<instr_n>       ::= "JC" | "JNC" | "JZ" | "JNZ" | "JS" | "JNS" | "JV" | "JNV" | "CALL"
<instr_op1>     ::= "JMP"
<instr_reg_op2> ::= "MOV" | "ADD" | "ROR" | "SUB" | "CMP" | "AND" | "OR" | "XOR"
<instr_ldst>    ::= "LOAD" | "STORE"
<instr_reg_n>   ::= "BIT"
<op1>           ::= <op2>
<bracket_op2>   ::= "[" <op2> "]"
<op2>           ::= <reg> | <n>
<n>             ::= <number> | <identifier>
<reg>           ::= "R0" | "R1" | "R2" | "R3" | "R4" | "R5" | "R6" | "SP" | "R7" | "FLAGS"
<array>         ::= <array_element> | <array_element> <,> <array>
<array_element> ::= <number> | <quoted_string>
<quoted_string> ::= "\"" <char+> "\""
<,>             ::= <s*> "," <s*>
<identifier>    ::= <letter> <name_char*>
<name_char*>    ::= <name_char> <name_char*> | ""
<name_char>     ::= <letter> | <dec> | "_"
<number>        ::= "0x" <hex+> | "0b" <bit+> | <dec+>
<hex+>          ::= <hex> | <hex> <hex+>
<dec+>          ::= <dec> | <dec> <dec+>
<bit+>          ::= <bit> | <bit> <bit+>
<hex>           ::= <dec> | "A" | "B" | "C" | "D" | "E" | "F" | "a" | "b" | "c" | "d" | "e" | "f"
<dec>           ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<bit>           ::= "0" | "1"
<char+>         ::= <char> | <char> <char+>
<char>          ::= <letter> | <dec> | " "
<[\n]>          ::= <nl+> | <s*>
<nl+>           ::= <nl> | <nl> <nl+>
<nl>            ::= <s*> "\n" <s*>
<s*>            ::= <s+> | ""
<s+>            ::= <s> | <s> <s+>
<s>             ::= " " | "\t"
<letter>        ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"